VARIABLE _SCORE
VARIABLE BONUS
VARIABLE NEXT-BONUS
VARIABLE LAST-ROLL
VARIABLE OPEN-FRAME
VARIABLE FRAME-COUNT

: START-GAME 
    _SCORE     OFF 
    BONUS      OFF 
    NEXT-BONUS OFF
    LAST-ROLL  OFF 
    OPEN-FRAME OFF 
    1 FRAME-COUNT ! ;

: SCORE 
    _SCORE @ ;

: ADVANCE-BONUS 
    NEXT-BONUS @ BONUS !
    NEXT-BONUS OFF ;

: COLLECT-BONUS ( pins -- )
    BONUS @ ?DUP IF
        * _SCORE +!
        ADVANCE-BONUS
    ELSE 
        DROP 
    THEN ;

: OPEN-FRAME? ( -- flag )
    OPEN-FRAME @ ;

: STRIKE? ( pins -- flag )
    10 = OPEN-FRAME? 0= AND ;

: SPARE? ( pins -- flag )
    LAST-ROLL @ + 10 = 
    OPEN-FRAME? AND ;

: PAST-10-FRAMES? ( -- flag )
    FRAME-COUNT @ 10 > ;

: STRIKE-BONUS 
    1 BONUS +!  1 NEXT-BONUS ! ;

: SPARE-BONUS
    1 BONUS ! ;

: CALC-BONUS
    PAST-10-FRAMES? IF DROP ELSE
        DUP STRIKE? IF 
            DROP
            STRIKE-BONUS 
            OPEN-FRAME ON
        ELSE 
            SPARE? IF SPARE-BONUS THEN
        THEN
    THEN ;

: ADVANCE-FRAME
    OPEN-FRAME? IF 1 FRAME-COUNT +! THEN
    OPEN-FRAME? 0= OPEN-FRAME ! ;

: ADD-PINS ( pins -- )
    PAST-10-FRAMES? IF DROP ELSE _SCORE +!  THEN ;

: ADD-ROLL ( pins -- )
    DUP COLLECT-BONUS
    DUP CALC-BONUS
    DUP LAST-ROLL !
    ADD-PINS
    ADVANCE-FRAME ;
